# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=polkit
pkgver=0.114
pkgrel=1.1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
depends=(glib2 pam expat systemd js52)
makedepends=(intltool gtk-doc gobject-introspection git autoconf-archive)
_commit=ed06baed179166389d536420a6fc532781d48178  # tags/0.114^0
source=("git+https://anongit.freedesktop.org/git/polkit#commit=$_commit"
        x11vars.patch)
sha256sums=('SKIP'
            '33e73eadec6b533ae41c30f571c997874c96165f0b2a80ce345253f13ee85af3')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n 373705b35e7f6c7dc83de5e0a3ce11ecd15d0409
  NOCONFIGURE=1 ./autogen.sh
  patch -Np1 -i $srcdir/x11vars.patch
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib \
      --enable-libsystemd-login=yes --disable-static \
      --enable-gtk-doc --with-os-type=redhat

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  chown root:102 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  chmod 750      "$pkgdir"/{etc,usr/share}/polkit-1/rules.d

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END
}
