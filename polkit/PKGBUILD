# $Id: PKGBUILD 249755 2015-10-25 18:39:57Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=polkit
pkgver=0.113+29+g3272a98
pkgrel=1.1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
depends=(glib2 pam expat systemd js)
makedepends=(intltool gtk-doc gobject-introspection git autoconf-archive)
install=polkit.install
_commit=3272a988655c3236b55bad70e9a3af20857f384b  # master
source=("git+https://anongit.freedesktop.org/git/polkit#commit=$_commit"
        'x11vars.patch')
md5sums=('SKIP'
         '6483b6cbde24d0b3eb3ef137db06e617')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i $srcdir/x11vars.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-1 \
      --enable-libsystemd-login=yes --disable-static \
      --enable-gtk-doc --with-os-type=redhat
      
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  
  make
}

check() {
  cd $pkgname
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  chown root:102 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  chmod 750      "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
}
