# Maintainer: Manuel Barrette (manb[at]manjaro[dot]org)
# Contributor: Jonathon Fernyhough (jonathon[at]manjaro[dot]org)

pkgname=('manjaro-documentation-fr')
pkgver=20170326
_gitcommit=98411b4ae524a147432b1e0a2d53bd34b63f3350
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-user-guide"
makedepends=('ghostscript' 'lyx' 'python2' 'texlive-core' 'texlive-latexextra' 'texlive-pictures' 'ttf-comfortaa')
source=("manjaro-documentation-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz"
	'manjaro-documentation'
	'manjaro-documentation.desktop')
sha256sums=('96038373ba21e10f40d24697fc9234b7b08803e25f6d5b1ddaf96cd0cfc905f2'
            '5ba5683ccd86e939e88b3f42a3c0fb492fb6d5330e8522abc4800853dc71af70'
            'cfdc3cc5fb759b835f2cb9bca830ea0ea48b4ab98b58867c0420930e890d74c6')

prepare() {
  cd ${srcdir}/manjaro-user-guide-$_gitcommit
  lyx --export pdflatex manjaro-user-guide.lyx
  pdflatex manjaro-user-guide
  texindy --language english manjaro-user-guide.idx
  pdflatex manjaro-user-guide
  pdflatex manjaro-user-guide
  gs -dNOPAUSE -dBATCH -sDEVICE=pdfwrite -dCompatibilityLevel=1.4 -dPDFSETTINGS=/printer \
  	-sOutputFile=manjaro-user-guide-printer.pdf manjaro-user-guide.pdf
}

package() {
  pkgdesc="Manjaro Beginner User Guide in French"
  license=('CC-BY-SA 4.0')
  provides=("manjaro-documentation=$pkgver")
  conflicts=('manjaro-documentation')
  install -Dm644 "$srcdir/manjaro-user-guide-$_gitcommit/manjaro-user-guide-printer.pdf" \
  	"$pkgdir/usr/share/doc/livecd/Beginner_User_Guide_Fr.pdf"
  install -Dm755 manjaro-documentation "$pkgdir/usr/bin/manjaro-documentation-fr"
  install -Dm644 manjaro-documentation.desktop "$pkgdir/usr/share/applications/manjaro-documentation-fr.desktop"
}
