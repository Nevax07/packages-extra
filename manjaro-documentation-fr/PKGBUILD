# Maintainer: Manuel Barrette (manb[at]manjaro[dot]org)
# Contributor: Jonathon Fernyhough (jonathon[at]manjaro[dot]org)

pkgname=('manjaro-documentation-fr')
pkgver=20180113
_gitcommit=f51aeb20d17b7c29247b30afa02e13420af7d8f2
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-user-guide"
makedepends=('ghostscript' 'lyx' 'python2' 'texlive-core' 'texlive-latexextra' 'texlive-pictures' 'ttf-comfortaa')
source=("manjaro-documentation-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz"
	'manjaro-documentation'
	'manjaro-documentation.desktop')
sha256sums=('bdfe0384060a1358f827adda37d8b94bb3dad73462620e6a1ba8302421c378c3'
            'fc9a1d22ba5a70ff95a52baadba1df4ec2a07f9474c8b20af24646f01b3783ea'
            'cfdc3cc5fb759b835f2cb9bca830ea0ea48b4ab98b58867c0420930e890d74c6')

prepare() {
  updmap-user --cnffile=/etc/texmf/web2c/updmap.cfg
  cd ${srcdir}/manjaro-user-guide-$_gitcommit
  lyx --export pdflatex manjaro-user-guide.lyx
  pdflatex manjaro-user-guide
  texindy --language english manjaro-user-guide.idx
  pdflatex manjaro-user-guide
  pdflatex manjaro-user-guide
  gs -dNOPAUSE -dBATCH -sDEVICE=pdfwrite -dCompatibilityLevel=1.4 -dPDFSETTINGS=/printer \
  	-sOutputFile=manjaro-user-guide-printer.pdf manjaro-user-guide.pdf
}

package() {
  pkgdesc="Manjaro Beginner User Guide in French"
  license=('CC-BY-SA 4.0')
  provides=("manjaro-documentation=$pkgver")
  conflicts=('manjaro-documentation')
  install -Dm644 "$srcdir/manjaro-user-guide-$_gitcommit/manjaro-user-guide-printer.pdf" \
  	"$pkgdir/usr/share/doc/manjaro/Beginner_User_Guide_Fr.pdf"
  install -Dm755 manjaro-documentation "$pkgdir/usr/bin/manjaro-documentation-fr"
  install -Dm644 manjaro-documentation.desktop "$pkgdir/usr/share/applications/manjaro-documentation-fr.desktop"
}
