# $Id: PKGBUILD 267171 2016-05-07 05:09:56Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.12.3
pkgrel=2.1
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'thunar' 'garcon' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        folder.patch::https://git.xfce.org/users/eric/xfdesktop/patch/?id=cc311b61b82b7510a3a6cb0952d3a331e3551e05
        dialog.patch::https://git.xfce.org/xfce/xfdesktop/patch/?id=b88672ffcfa0ab55d805bba5a089fb0056fc26a6)
sha256sums=('a8a8d93744d842ca6ac1f9bd2c8789ee178937bca7e170e5239cbdbef30520ac'
            '6142504cbc8591839c207d3d6e95458588fdd78ca681235353b71d2dd7a4cbf5'
            '487b78ad3c1fa5384fb8bd4f65879ee7c8863ea31ba7c58560195cca52c7904b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/folder.patch"
  patch -Np1 -i "$srcdir/dialog.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
