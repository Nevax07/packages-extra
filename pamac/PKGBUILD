# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgbase=pamac
pkgname=('pamac' 'pamac-tray-appindicator')
pkgver=4.3.5
_pkgver=4.3.5
pkgrel=2
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'libappindicator-gtk3')
options=(!emptydirs)

source=("pamac-$pkgver-$pkgrel.tar.gz::$url/archive/v$_pkgver.tar.gz"
        "improve-daemon-quit.patch::https://github.com/manjaro/pamac/commit/f772be911c9814ece22a1a0d1710f8e6e12adcdc.patch"
        "show-tray-icon.patch::https://github.com/manjaro/pamac/commit/cb5d07156a31602f6c701973c05df0e1030f8aae.patch")
sha256sums=('8e9347fcc236789e433c93aa5acab784acf7943e3ee5edc1957ee1f75811194b'
            '041a973b2262ae63f8357e6e18e7ed0ef94f7a331b1bbae22006ca537639fb0e'
            'f63a7e1a7591f96b96206b9fca702c24b48f50b674608c97c2dc246f28328764')
  
prepare() {
  # adjust version string
  cd "$srcdir/pamac-$_pkgver"
  sed -i -e "s|\"$pkgver\"|\"$pkgver-$pkgrel\"|g" src/transaction.vala
  # patches here
  patch -p1 -i "$srcdir/improve-daemon-quit.patch"
  patch -p1 -i "$srcdir/show-tray-icon.patch"
}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all
}

package_pamac() {
  optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
              'lxsession: needed for authentification in Xfce, LXDE etc.'
              'pamac-tray-appindicator: tray icon for KDE')
  backup=('etc/pamac.conf')
  replaces=('pamac-aur')
  install=pamac.install
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}

package_pamac-tray-appindicator() {
  pkgdesc="Tray icon using appindicator which feets better in KDE"
  depends=('pamac' 'libappindicator-gtk3')
  cd "$srcdir/pamac-$_pkgver"
  make use_appindicator=true
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install_pamac-tray-appindicator
}
# vim:set ts=2 sw=2 et:
