# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=5.5
pkgrel=1.1
pkgdesc="Braille display driver for Linux/Unix"
arch=(i686 x86_64)
url="http://mielke.cc/brltty"
license=(GPL LGPL)
depends=(libxaw gpm icu tcl bluez-libs espeak)
makedepends=(at-spi2-core cython tcl speech-dispatcher)
optdepends=('at-spi2-core: X11/GNOME Apps accessibility'
            'atk: ATK bridge for X11/GNOME accessibility')
backup=(etc/brltty.conf)
options=('!emptydirs')
install=brltty.install
source=(http://mielke.cc/brltty/archive/brltty-$pkgver.tar.xz
        brltty.service)
md5sums=('cd2fb2158b9fc85b23c4225d1d067df6'
         '194b8292e0f26a15c7973e285be928cb')
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --mandir=/usr/share/man \
    --with-tables-directory=/usr/share/brltty \
    --with-screen-driver=lx \
    --enable-gpm \
    --disable-java-bindings \

  make -j1
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 Documents/brltty.conf "$pkgdir/etc/brltty.conf"
  install -Dm644 Authorization/Polkit/org.a11y.brlapi.policy "$pkgdir/usr/share/polkit-1/actions/org.a11y.brlapi.policy"
  install -Dm644 ../brltty.service "$pkgdir/usr/lib/systemd/system/brltty.service"
  sed -i '/api-parameters Auth=polkit/s/^#//' $pkgdir/etc/brltty.conf
}

