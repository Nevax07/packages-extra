# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgbase=pamac
pkgname=('pamac-dev' 'pamac-dev-tray-appindicator')
pkgver=6.0.3
_pkgver=6.0.3
_commit=78b82572dc5bcbace11f637b3a185afbdc052730
pkgrel=3
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4'
         'appstream-glib' 'archlinux-appstream-data')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'libappindicator-gtk3')
options=(!emptydirs)

source=(#"pamac-$pkgver-$pkgrel.tar.gz::https://github.com/manjaro/pamac/archive/v$_pkgver.tar.gz"
        "pamac-$pkgver-$pkgrel.tar.gz::https://github.com/manjaro/pamac/archive/$_commit.tar.gz")
sha256sums=('90414d391ce75471d11638b1ac922bea705d022c2516cc5d00d86cab37029aa2')
  
prepare() {
  mv "$srcdir/pamac-$_commit" "$srcdir/pamac-$_pkgver"
  cd "$srcdir/pamac-$_pkgver"
  # patches here

  # adjust version string
  sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala
}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all
}

package_pamac-dev() {
  optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
              'lxsession: needed for authentification in Xfce, LXDE etc.'
              'pamac-dev-tray-appindicator: tray icon for KDE')
  conflicts=('pamac')
  provides=('pamac')
  backup=('etc/pamac.conf')
  replaces=('pamac-aur')
  install=pamac.install
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}

package_pamac-dev-tray-appindicator() {
  pkgdesc="Tray icon using appindicator which feets better in KDE"
  depends=('pamac-dev' 'libappindicator-gtk3')
  conflicts=('pamac-tray-appindicator')
  provides=('pamac-tray-appindicator')
  cd "$srcdir/pamac-$_pkgver"
  make use_appindicator=true
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install_pamac-tray-appindicator
}
# vim:set ts=2 sw=2 et:
