# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

_pkgname=thunar-archive-plugin
pkgname=$_pkgname-gtk3
pkgver=0.4.0
pkgrel=1
_commit=b63862f03a041c2467c18dc8828f3a63a2d00328
pkgdesc="Create and extract archives in Thunar"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-archive-plugin"
license=('GPL2')
groups=('xfce4-gtk3-goodies')
depends=('thunar-gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=(${_pkgname})
optdepends=('file-roller'
            'kdeutils-ark'
            'xarchiver')
source=(https://git.xfce.org/thunar-plugins/${_pkgname}/snapshot/${_pkgname}-${_commit}.tar.bz2)
sha256sums=('55bb250945c1cbb459c315e9d8aabafd57c2de92fd53c5f2833da9cbd8917610')

prepare() {
  cd "$srcdir/${_pkgname}-${_commit}"

  NOCONFIGURE=1 xdt-autogen
}

build() {
  cd "$srcdir/${_pkgname}-${_commit}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd "$srcdir/${_pkgname}-${_commit}"
  make DESTDIR="$pkgdir" install
}
