# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

_pkgname=thunar-archive-plugin
pkgname=$_pkgname-gtk3
pkgver=0.3.1
pkgrel=1
_commit=cf91e938eacf9291f9f2616e48cbd70a501724be
pkgdesc="Create and extract archives in Thunar"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-archive-plugin"
license=('GPL2')
groups=('xfce4-gtk3-goodies')
depends=('thunar-gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=(${_pkgname})
replaces=(${_pkgname})
optdepends=('file-roller'
            'kdeutils-ark'
            'xarchiver')
source=(https://git.xfce.org/thunar-plugins/${_pkgname}/snapshot/${_pkgname}-${_commit}.tar.bz2
        0001-Fix-file-roller-3.12.1-detection-bug-10826.patch
	patch1.patch::https://github.com/andreldm/thunar-archive-plugin/commit/3ab18ce256a8b66d0d294b3222e3b76cc5d884e5.patch
	patch2.patch::https://github.com/andreldm/thunar-archive-plugin/commit/3b7c8d10e4242d4938657fac9bd0204aaa0c2217.patch
	patch3.patch::https://github.com/andreldm/thunar-archive-plugin/commit/5083c2bc5cc78e8ebe69866a262a21353cb1830d.patch
	patch4.patch::https://github.com/andreldm/thunar-archive-plugin/commit/9d5de649793810f76eae8a18fd3227acb9c26a07.patch
	patch5.patch::https://github.com/andreldm/thunar-archive-plugin/commit/faedc3bc5ca6ba10e378c0ff6d5e0666322d5294.patch
	)
sha256sums=('8515f3e9ad9423f0b81c28e78e6319e67d0ad193d6b255c36209a41679c0e63f'
            '4373c3011f5abd1d4119d283e832aeb858c810f14b3a59b6250cf10893aa0757'
            '795c693ff65bcd95b70fcbab3849569d7b99723f918eb892d5916b90b145ee21'
            'eed45c7b9cf831b4ab83366fe5855bc86f69ed55baa4ae1ecfc09f7ea69ce3ff'
            '48457986dc8982b9296cb455a6862dab5052968a59e1fe6d834d3a0541032aa8'
            '837e86e47ef8065e32e17a6dd8fb7e0a638c7c033e5d40f88e1b05373686c821'
            'f35067f37649ee0161bcc291a9550e658efe4b4394d0f5983d5ed9d8e92750ad')

prepare() {
  cd "$srcdir/${_pkgname}-${_commit}"

  # https://bugzilla.xfce.org/show_bug.cgi?id=10826
  patch -Np1 -i ../0001-Fix-file-roller-3.12.1-detection-bug-10826.patch
  patch -Np1 -i ../patch1.patch
  patch -Np1 -i ../patch2.patch
  patch -Np1 -i ../patch3.patch
  patch -Np1 -i ../patch4.patch
  patch -Np1 -i ../patch5.patch
  NOCONFIGURE=1 xdt-autogen
}

build() {
  cd "$srcdir/${_pkgname}-${_commit}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd "$srcdir/${_pkgname}-${_commit}"
  make DESTDIR="$pkgdir" install
}
