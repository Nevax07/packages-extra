# $Id: PKGBUILD 273439 2016-08-07 09:02:23Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings-gtk3
_pkgname=xfce4-settings
pkgver=4.13.1
pkgrel=1
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3' 'garcon-gtk3' 'libxfce4ui-gtk3' 'libnotify' 'libxklavier'
         'gnome-icon-theme' 'gnome-themes-standard')
makedepends=('intltool' 'xf86-input-libinput' 'xfce4-dev-tools')
optdepends=('libcanberra: for sound control')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=3effeb4908b8d3ed00b11b5fcac27b745500c9f4
source=(https://git.xfce.org/xfce/xfce4-settings/snapshot/xfce4-settings-$_commit.tar.bz2
        default-xsettings-xml.patch)
sha256sums=('c5d9d1794f16ad931103d2aa1e716cf7ba44e081c30fcf497247c25f85043d2b'
            '1c4110fd5ce0f17564cda892b72d59a87b515fdb4580c7154bbb4c1979f5ade7')
prepare() {
  cd "$srcdir/$_pkgname-$_commit"

  # Enable GNOME icon theme, Adwaita theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-upower-glib \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
