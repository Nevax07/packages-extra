# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgname=garcon
pkgname=$_pkgname-gtk3
pkgver=0.6.1
pkgrel=2
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('LGPL')
groups=('xfce4-gtk3')
depends=('libxfce4ui-gtk3')
makedepends=('intltool' 'xfce4-dev-tools')
replaces=('libxfce4menu')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=37ffacbcbd2c2fd37eb05dd851f05ca8ae7952f1
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('302e38af392a69b55e38bf723c23c18f3aa954ee3cec12f28f81788d94019e42')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
