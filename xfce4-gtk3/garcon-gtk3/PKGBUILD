# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgname=garcon
pkgname=$_pkgname-gtk3
pkgver=0.6.1
pkgrel=4
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('LGPL')
groups=('xfce4-gtk3')
depends=('libxfce4ui-gtk3')
makedepends=('intltool' 'xfce4-dev-tools')
replaces=('libxfce4menu')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=3b18d273bc5df61edc7f9b4f9f59ed20dd065014
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('63102cafe11dd6817c630922fd5a6b9a5bd95ce102a9d541b90b1752266ae922')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
