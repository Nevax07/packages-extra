# $Id: PKGBUILD 232147 2015-03-01 06:33:54Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=garcon-gtk3
_pkgname=garcon
pkgver=0.6.1
pkgrel=1
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('LGPL')
groups=('xfce4-gtk3')
depends=('libxfce4ui-gtk3')
makedepends=('intltool' 'xfce4-dev-tools')
replaces=('libxfce4menu')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=2e640f89f8538379bacffb87a1f01c7d5d68909c
source=(https://git.xfce.org/xfce/garcon/snapshot/garcon-$_commit.tar.bz2)
sha256sums=('0158cb699844d2207a040304496eef87bef5940950de2a7b4b6b0f4a2ca61778')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
