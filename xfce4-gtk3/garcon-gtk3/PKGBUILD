# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgname=garcon
pkgname=$_pkgname-gtk3
pkgver=0.6.1
pkgrel=1
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('LGPL')
groups=('xfce4-gtk3')
depends=('libxfce4ui-gtk3')
makedepends=('intltool' 'xfce4-dev-tools')
replaces=('libxfce4menu')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=0ab374d3225876397cb69f64612a46b3540082f0
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('4f9b0a3c4316e5609f8864c79ae5d4607b571105969f11c8ff850bf4dbbb2ee9')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
