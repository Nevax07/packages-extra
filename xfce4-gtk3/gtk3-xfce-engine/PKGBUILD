# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=gtk3-xfce-engine
pkgver=3.2.0
pkgrel=1
pkgdesc="Xfce gtk3 engine and themes"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('gtk3')
conflicts=("${pkgname//3}")
provides=("${pkgname//3}")
source=("http://archive.xfce.org/src/xfce/${pkgname//3}/${pkgver%.*}/${pkgname//3}-$pkgver.tar.bz2")
md5sums=('363d6c16a48a00e26d45c45c2e1fd739')

build() {
  cd "$srcdir/${pkgname//3}-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/${pkgname//3}-$pkgver"
  make DESTDIR="$pkgdir" install
}
