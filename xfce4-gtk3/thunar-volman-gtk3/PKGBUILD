# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

_pkgname=thunar-volman
_commit=26efa1b6bf0df3d7517a0e91a7b854609eed36fe
pkgname=thunar-volman-gtk3
pkgver=0.9.0
pkgrel=1
pkgdesc="Automatic management of removeable devices in Thunar"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-volman"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('thunar-gtk3' 'libxfce4ui-gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=($_pkgname)
provides=($_pkgname)
source=(https://git.xfce.org/xfce/thunar-volman/snapshot/thunar-volman-$_commit.tar.gz)
sha256sums=('47084eda3bb6fa4fe03643572035ae8d0a5af552317bd48159400f94160faf34')

build() {
  cd "$srcdir/$_pkgname-$_commit"
  NOCONFIGURE=1 ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
