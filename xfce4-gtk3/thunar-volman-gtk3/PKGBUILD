# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

_pkgname=thunar-volman
_commit=8e42d3fe1d63d3bd3c87a307d2fa01fc02350840
pkgname=thunar-volman-gtk3
pkgver=0.8.1
pkgrel=2
pkgdesc="Automatic management of removeable devices in Thunar"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-volman"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('thunar-gtk3' 'libxfce4ui-gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=($_pkgname)
provides=($_pkgname)
source=(https://git.xfce.org/xfce/thunar-volman/snapshot/thunar-volman-$_commit.tar.gz)
sha256sums=('a55098dd9dbdcb90c5807779d3343320e984db040feb4581a1489d8364f1fe50')

build() {
  cd "$srcdir/$_pkgname-$_commit"
  NOCONFIGURE=1 ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
