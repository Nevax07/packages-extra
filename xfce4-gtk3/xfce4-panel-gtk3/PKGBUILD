# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-panel
pkgname="$_pkgname-gtk3"
pkgver=4.13.2
pkgrel=1
pkgdesc="Panel for the Xfce desktop environment"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('desktop-file-utils'
    'exo-gtk3'
    'garcon-gtk3'
    'hicolor-icon-theme'
    'libwnck3'
    'libxfce4ui-gtk3'
    'vala'
    'xfconf-gtk3>=4.13.3')
makedepends=('intltool' 'xfce4-dev-tools')
_commit=880f91a20d6c61df154aac44e2e6d76157162277
provides=($_pkgname)
conflicts=($_pkgname)
source=(https://git.xfce.org/xfce/xfce4-panel/snapshot/xfce4-panel-$_commit.tar.bz2)
sha256sums=('95fc532cfd58382c9da334dc6d26c91a5b64534549407569843042c8d621f945')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-gtk3 \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
