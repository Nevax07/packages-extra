# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-panel-gtk3
_pkgname=xfce4-panel
pkgver=4.13.0
pkgrel=1.4
pkgdesc="Panel for the Xfce desktop environment"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3' 'garcon-gtk3' 'libxfce4ui-gtk3' 'libwnck3' 'hicolor-icon-theme'
         'desktop-file-utils')
makedepends=('intltool' 'gtk-doc' 'xfce4-dev-tools')
_commit=18a9f030d25f4c9d22af1233ab6dff7ec897cf96
provides=($_pkgname)
conflicts=($_pkgname)
source=(https://git.xfce.org/xfce/xfce4-panel/snapshot/xfce4-panel-$_commit.tar.bz2)
sha256sums=('9fae22c87d8bbf8a08c747b332eaeaf982edad80c540e7575caa152c37e53500')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-gtk-doc \
    --enable-gtk3 \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

