# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-panel-gtk3
_pkgname=xfce4-panel
pkgver=4.13.0
pkgrel=2
pkgdesc="Panel for the Xfce desktop environment"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('desktop-file-utils'
    'exo-gtk3'
    'garcon-gtk3'
    'hicolor-icon-theme'
    'libwnck3'
    'libxfce4ui-gtk3')
makedepends=('intltool' 'gtk-doc' 'xfce4-dev-tools')
_commit=075a85dece34ddfedb455e07971e66dde77072c0
provides=($_pkgname)
conflicts=($_pkgname)
source=(https://git.xfce.org/xfce/xfce4-panel/snapshot/xfce4-panel-$_commit.tar.bz2)
sha256sums=('0f492b79a21dd052a2c65dbec2addca5f7b9e481726bf2249edfaefaa3e7d317')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-gtk-doc \
    --enable-gtk3 \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

