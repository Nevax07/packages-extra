# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-panel
pkgname="$_pkgname-gtk3"
pkgver=4.13.1
pkgrel=5
pkgdesc="Panel for the Xfce desktop environment"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('desktop-file-utils'
    'exo-gtk3'
    'garcon-gtk3'
    'hicolor-icon-theme'
    'libwnck3'
    'libxfce4ui-gtk3'
    'vala'
    'xfconf-gtk3>=4.13.3')
makedepends=('intltool' 'xfce4-dev-tools')
_commit=991f427fe727c749107f52c675be9dd7f9b284e8
provides=($_pkgname)
conflicts=($_pkgname)
source=(https://git.xfce.org/xfce/xfce4-panel/snapshot/xfce4-panel-$_commit.tar.bz2)
sha256sums=('e32036edf259e6cdbf09c9b47de6707afa0154cc79ef305ec9beaf6b6a22d619')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-gtk3 \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
