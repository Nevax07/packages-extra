# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-appfinder
pkgname=$_pkgname-gtk3
pkgver=4.13.0
pkgrel=2
pkgdesc="An application finder for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3' 'garcon-gtk3' 'hicolor-icon-theme' 'libxfce4ui-gtk3')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=("$_pkgname")
provides=("$_pkgname")
_commit=3d707ce8b59411fa9f265a6aa706b88f16427d2c
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('b7a35d2be3556cbcf938643a837fc21bf3b5184d0d78f7b893277399f37d88b4')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
