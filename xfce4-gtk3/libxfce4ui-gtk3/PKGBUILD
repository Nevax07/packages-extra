# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgname=libxfce4ui-gtk3
_pkgname=libxfce4ui
pkgver=4.13.3
pkgrel=2
pkgdesc="Commonly used Xfce widgets among Xfce applications (GTK3)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('gtk2'
    'gtk3'
    'hicolor-icon-theme'
    'libsm'
    'libxfce4util-gtk3'
    'startup-notification'
    'xfconf-gtk3')
makedepends=('intltool' 'xfce4-dev-tools' 'exo-gtk3')
conflicts=($_pkgname)
provides=("$_pkgname=$pkgver")
_commit=bffab34c0b8d17b6abd3889fa016dade90e56b0a
source=("https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2")
sha256sums=('118b17d1fb33cd0cb069dad20e9e42676e40b18162a9b5e90e6984597a9f6a8d')

build() {
  cd $_pkgname-$_commit

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --with-vendor-info="Manjaro-Linux"
  make
}

package() {
  cd $_pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
