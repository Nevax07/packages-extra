# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=libxfce4ui-gtk3
_pkgname=libxfce4ui
pkgver=4.13.3
pkgrel=1
pkgdesc="Commonly used Xfce widgets among Xfce applications (GTK3)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('gtk2'
    'gtk3'
    'hicolor-icon-theme'
    'libsm'
    'libxfce4util-gtk3'
    'startup-notification'
    'xfconf-gtk3')
makedepends=('intltool' 'gtk-doc' 'xfce4-dev-tools' 'exo-gtk3')
conflicts=($_pkgname)
provides=("$_pkgname=$pkgver")
#_commit=ccb1ee69ff7447835018b28d07470eeefa10157c
#source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
source=(http://archive.xfce.org/src/xfce/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2)
sha256sums=('815c33a49392520feb84f119cd8f558632a735e322ca55edce33f815a0624470')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gtk-doc \
    --disable-debug \
    --with-vendor-info="Manjaro-Linux"
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
