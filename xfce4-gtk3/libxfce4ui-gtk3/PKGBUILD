# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgname=libxfce4ui-gtk3
_pkgname=libxfce4ui
pkgver=4.13.4
pkgrel=1
pkgdesc="Commonly used Xfce widgets among Xfce applications (GTK3)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('gtk2'
    'gtk3'
    'hicolor-icon-theme'
    'libsm'
    'libxfce4util-gtk3'
    'startup-notification'
    'xfconf-gtk3')
makedepends=('intltool' 'xfce4-dev-tools' 'exo-gtk3')
conflicts=($_pkgname)
provides=("$_pkgname=$pkgver")
_commit=a663a5344d405eb93192ada5e0990ee9a5269b4c
source=("https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2")
sha256sums=('eec9c2521cecf7fd406a5f8daa65c4dd76e9e6493fbb4431eae0b58f36d9faea')

build() {
  cd $_pkgname-$_commit

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --with-vendor-info="Manjaro-Linux"
  make
}

package() {
  cd $_pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
