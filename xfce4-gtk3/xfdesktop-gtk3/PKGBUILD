# $Id: PKGBUILD 267171 2016-05-07 05:09:56Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop-gtk3
_pkgname=xfdesktop
pkgver=4.13.1
pkgrel=3
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3'
    'garcon-gtk3'
    'hicolor-icon-theme'
    'libwnck3'
    'libxfce4ui-gtk3'
    'thunar-gtk3')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=('xfce4-menueditor' "$_pkgname")
provides=("$_pkgname")
replaces=('xfce4-menueditor')
_commit=424208b3e150c48797faeeac7693c5ad6594b3e8
source=(https://git.xfce.org/xfce/xfdesktop/snapshot/xfdesktop-$_commit.tar.bz2)
sha256sums=('10bb87124f9f9c2c0e48dfa08ad2a8b0739c785986bb004da1f5cfad7dfdf44b')

prepare() {
  cd "$srcdir/$_pkgname-$_commit"
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
