# $Id: PKGBUILD 267171 2016-05-07 05:09:56Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop-gtk3
_pkgname=xfdesktop
pkgver=4.13.0
pkgrel=1.4
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('libxfce4ui-gtk3' 'thunar' 'garcon-gtk3' 'hicolor-icon-theme' 'libwnck3')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=('xfce4-menueditor' "$_pkgname")
provides=("$_pkgname")
replaces=('xfce4-menueditor')
_commit=f05c1a4f7d84439071694482543004013a44c775
source=(https://git.xfce.org/xfce/xfdesktop/snapshot/xfdesktop-$_commit.tar.bz2)
sha256sums=('58bb9181216f75d5396c84cc117a7f7a0c6ace620540e5f311e38c4ffeb77632')

prepare() {
  cd "$srcdir/$_pkgname-$_commit"
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
