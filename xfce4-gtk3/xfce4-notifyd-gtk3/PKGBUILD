# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-notifyd
_commit=2cb9617601b959e18c0bbb42a808515c7defa4a8
pkgname=$_pkgname-gtk3
pkgver=0.4.1
pkgrel=2
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-gtk3-goodies')
depends=('exo-gtk3'
    'hicolor-icon-theme'
    'libnotify'
    'libxfce4ui-gtk3')
makedepends=('intltool' 'python' 'xfce4-dev-tools' 'xfce4-panel-gtk3')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=(https://git.xfce.org/apps/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('2fb810ab42bfd2c4bad29a4b3b77a38d4bdd32f853e59d72878157604c7577d2')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
