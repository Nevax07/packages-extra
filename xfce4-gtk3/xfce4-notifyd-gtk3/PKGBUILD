# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-notifyd
_commit=8cc2825d454108d3fd99086480bb8c6ca59b886b
pkgname=$_pkgname-gtk3
pkgver=0.4.0
pkgrel=3
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-gtk3-goodies')
depends=('exo-gtk3'
    'hicolor-icon-theme'
    'libnotify'
    'libxfce4ui-gtk3')
makedepends=('intltool' 'python' 'xfce4-dev-tools' 'xfce4-panel-gtk3')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=(https://git.xfce.org/apps/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('ae16c97601cc23660067f63becb6452914bc9c536d76aba0ed8a3f5acae7c453')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
