# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-notifyd
_commit=060f378ffee1acf7ca9a4b1763953ed5888b12a6
pkgname=$_pkgname-gtk3
pkgver=0.3.6
pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-goodies-gtk3')
depends=('exo-gtk3' 'libxfce4ui-gtk3' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python' 'xfce4-dev-tools')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=(https://git.xfce.org/apps/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('37149c7119cba9df3b6310d9eb9efea7315fd1acb255e756c3218549a2f0a4e8')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
