# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-notifyd
_commit=7a0206b1501c83aa4bd5530025c93a770cb7d6d6
pkgname=$_pkgname-gtk3
pkgver=0.3.6
pkgrel=2
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-goodies-gtk3')
depends=('exo-gtk3' 'libxfce4ui-gtk3' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python' 'xfce4-dev-tools')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=(https://git.xfce.org/apps/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('ebaba23673e1e94212891b46af1b2d42a8810c49b25d6ba100c8fb31b2f2796e')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
