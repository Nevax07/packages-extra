# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfconf-gtk3
_pkgname=xfconf
pkgver=4.13.4
pkgrel=1
pkgdesc="A simple client-server configuration storage and query system"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('dbus-glib' 'libxfce4util-gtk3')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig' 'glib-perl'
             'intltool' 'chrpath' 'xfce4-dev-tools')
options=('!emptydirs')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=3bbbb3da63a26f7dfc0326af69fce82b285d7230
source=(https://git.xfce.org/xfce/xfconf/snapshot/xfconf-$_commit.tar.bz2)
sha256sums=('65ddd6e5b0f76a6f5b58596ec9cba4e8ad530c1fafcf7167707daa7ac2140ce4')

prepare() {
  cd "$srcdir/$_pkgname-$_commit"
  # patches here
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"

  make DESTDIR="$pkgdir" install

  # Fix insecure rpath, http://bugs.archlinux.org/task/19980
  find "$pkgdir" -name Xfconf.so -exec chrpath -d {} +

  # Backward compatibility with applications linked to Xfconf v2.0.0.
  cd "$pkgdir/usr/lib"
  ln -s libxfconf-0.so libxfconf-0.so.2.0.0
  ln -s libxfconf-0.so libxfconf-0.so.2
}
