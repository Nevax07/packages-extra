# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfconf-gtk3
_pkgname=xfconf
pkgver=4.13.4
pkgrel=2
pkgdesc="A simple client-server configuration storage and query system"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('dbus-glib' 'libxfce4util-gtk3')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig' 'glib-perl'
             'intltool' 'chrpath' 'xfce4-dev-tools')
options=('!emptydirs')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=c38105c0987c0ed997fe24c486ad1de55610b0f5
source=(https://git.xfce.org/xfce/xfconf/snapshot/xfconf-$_commit.tar.bz2)
sha256sums=('dd2153a984bd9b2df8b5d4b0b70872be3cc504ab6f436b57fd637d2da9fb95c0')

prepare() {
  cd "$srcdir/$_pkgname-$_commit"
  # patches here
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"

  make DESTDIR="$pkgdir" install

  # Fix insecure rpath, http://bugs.archlinux.org/task/19980
  find "$pkgdir" -name Xfconf.so -exec chrpath -d {} +
}
