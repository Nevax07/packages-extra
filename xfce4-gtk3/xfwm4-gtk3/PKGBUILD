# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfwm4-gtk3
_pkgname=xfwm4
pkgver=4.13.0
pkgrel=1
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3' 'hicolor-icon-theme' 'libxfce4ui-gtk3' 'libwnck3' 'libdrm' 'libxpresent')
makedepends=('intltool' 'xfce4-dev-tools')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=dec64dd6775125603150a121802b82ca60931801
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('7e95f469928b2747bed3c3a37c2ce81fb404dee298524efe56d60e7b5e42b419')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-epoxy \
    --enable-startup-notification \
    --enable-xsync \
    --enable-render \
    --enable-randr \
    --enable-xpresent \
    --enable-compositor \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
