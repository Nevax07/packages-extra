# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfwm4-gtk3
_pkgname=xfwm4
pkgver=4.13.0
pkgrel=11
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3' 'hicolor-icon-theme' 'libxfce4ui-gtk3' 'libwnck3' 'libdrm' 'libxpresent')
makedepends=('intltool' 'xfce4-dev-tools')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=e4b8d96f1d1270ff2be3ba9c6ada10fb7c7cb4df
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('0358162d5a06cb9f8fe2e134fdc482485363c0da9bebe4b71d8050a0e0edc693')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-epoxy \
    --enable-startup-notification \
    --enable-xsync \
    --enable-render \
    --enable-randr \
    --enable-xpresent \
    --enable-compositor \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
