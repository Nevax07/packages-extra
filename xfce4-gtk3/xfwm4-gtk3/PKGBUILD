# $Id: PKGBUILD 290944 2017-03-16 17:44:37Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfwm4-gtk3
_pkgname=xfwm4
pkgver=4.13.0
pkgrel=2
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3' 'hicolor-icon-theme' 'libxfce4ui-gtk3' 'libwnck' 'libdrm')
makedepends=('intltool' 'xfce4-dev-tools')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=804e3528aa66c79ca97d1a5042b5cf17717199c9
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('e30ea35cdebafcc5b63032fcf32c575db402bf10907aadce3be4e086949bfe63')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification \
    --enable-randr \
    --enable-compositor \
    --enable-xsync \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
