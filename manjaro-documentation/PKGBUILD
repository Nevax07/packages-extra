# Maintainer: Manuel Barrette (manb[at]manjaro[dot]org)
# Contributor: Jonathon Fernyhough (jonathon[at]manjaro[dot]org)

pkgname=('manjaro-documentation-en')
pkgver=20171027
_gitcommit=4c72c732625bead932c1c59459cfa5716f0c3cdf
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-user-guide"
makedepends=('ghostscript' 'lyx' 'python2' 'texlive-core' 'texlive-latexextra' 'texlive-pictures' 'ttf-comfortaa')
source=("manjaro-documentation-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz"
	'manjaro-documentation'
	'manjaro-documentation.desktop')
sha256sums=('9fdb3ede16f73c3509f673833591f43e908ab5c20815c9da1baaed1d582db033'
            '018c1738e072605784b6ade39de64c03668e5e260554d82b6df9603563d59476'
            'cccdd31319b8157162a42d80e41b5606dce9aeb30587eca0d8b0e056b3b4717a')

prepare() {
  cd ${srcdir}/manjaro-user-guide-$_gitcommit
  lyx --export pdflatex manjaro-user-guide.lyx
  pdflatex manjaro-user-guide
  texindy --language english manjaro-user-guide.idx
  pdflatex manjaro-user-guide
  pdflatex manjaro-user-guide
  gs -dNOPAUSE -dBATCH -sDEVICE=pdfwrite -dCompatibilityLevel=1.4 -dPDFSETTINGS=/printer \
  	-sOutputFile=manjaro-user-guide-printer.pdf manjaro-user-guide.pdf
}

package() {
  pkgdesc="Manjaro Beginner User Guide in English"
  license=('CC-BY-SA 4.0')
  provides=("manjaro-documentation=$pkgver")
  replaces=('manjaro-documentation')
  conflicts=('manjaro-documentation')
  install -Dm644 "$srcdir/manjaro-user-guide-$_gitcommit/manjaro-user-guide-printer.pdf" \
  	"$pkgdir/usr/share/doc/manjaro/Beginner_User_Guide.pdf"
  install -Dm755 manjaro-documentation "$pkgdir/usr/bin/manjaro-documentation"
  install -Dm644 manjaro-documentation.desktop "$pkgdir/usr/share/applications/manjaro-documentation.desktop"
}
