# $Id: PKGBUILD 248535 2015-10-08 19:49:55Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=cogl
pkgver=1.22.2+11+g811f285a
pkgrel=1
pkgdesc="An object oriented GL/GLES Abstraction/Utility Layer"
url="https://blogs.gnome.org/clutter/"
arch=(i686 x86_64)
license=(GPL2)
depends=(mesa libdrm libxext libxdamage libxcomposite gdk-pixbuf2 pango libxrandr)
makedepends=(gobject-introspection git gtk-doc)
replaces=('cogl-catalyst')
_commit=811f285a8e7f20edd435d7d641b4db391dbf45ed  # cogl-1.22
source=("git+https://git.gnome.org/browse/cogl#commit=$_commit"
        gl_Add_a_quirk_for_the_GLSL_version_to_use_on_AMD_v2.patch
        Use_better_vendor_strings_from_AMD.patch)
sha256sums=('SKIP'
            'bed12e762657b2a864fd3abe8722e8acf436576d8fd1e7325d69590294ac72b0'
            '47a2ed89b5c6c1e3a6e5b64c97ae4dc000290b6bc9ef18b276934de4d29e8933')

pkgver() {
  cd $pkgname
  git describe | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # https://bugzilla.gnome.org/show_bug.cgi?id=756306
  patch -Np1 -i ../gl_Add_a_quirk_for_the_GLSL_version_to_use_on_AMD_v2.patch
  patch -Np1 -i ../Use_better_vendor_strings_from_AMD.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --enable-gles{1,2} \
    --enable-{kms,wayland}-egl-platform \
    --enable-wayland-egl-server

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
