# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Tomasz GÄ…sior <tomaszgasior.pl>

# This file is based on original PKGBUILD of GTK3 package.
# https://git.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/gtk3

__arch_pkg_commit="712b5a879d9389efba706c8817607fa65ac562e9"
_repo=gtk3-mushrooms

pkgname=gtk3-classic
pkgver=3.22.21
pkgrel=5
pkgdesc="GTK3 library modified for classic GTK-based desktops."
url="https://github.com/TomaszGasior/gtk3-mushrooms"
conflicts=(gtk3)
provides=("gtk3=$pkgver")
arch=(i686 x86_64)
license=(LGPL)
install=$pkgname.install
depends=(
    'adwaita-icon-theme'
    'atk'
    'at-spi2-atk'
    'cairo'
    'desktop-file-utils'
    'gdk-pixbuf2'
    'gtk-update-icon-cache'
    'json-glib'
    'libepoxy'
    'librsvg'
    'libxcomposite'
    'libxcursor'
    'libxdamage'
    'libxi'
    'libxinerama'
    'libxkbcommon'
    'libxrandr'
    'mesa'
    'pango'
    'shared-mime-info'
    'wayland'
    'wayland-protocols'
)
optdepends=(
    'gtk3-print-backends: printing'
    'dconf: default GSettings backend'
    'cantarell-fonts: default font'
)
makedepends=(
    'git'
    'gtk-doc'
    'gobject-introspection'
    'libcanberra' 
)
source=(
    # GTK source code.
    "https://download.gnome.org/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
    # ArchLinux package files.
    "settings.ini::https://git.archlinux.org/svntogit/packages.git/plain/trunk/settings.ini?h=packages/gtk3&id=$__arch_pkg_commit"
    "gtk-query-immodules-3.0.hook::https://git.archlinux.org/svntogit/packages.git/plain/trunk/gtk-query-immodules-3.0.hook?h=packages/gtk3&id=$__arch_pkg_commit"
    # gtk3-mushrooms patches
    "git+$url.git#commit=$_commit"
)
sha256sums=('1bd3c1a85cfb4db112cabb5379abb05a1a94fe43052d309d573493fca00e6b87'
            '01fc1d81dc82c4a052ac6e25bf9a04e7647267cc3017bc91f9ce3e63e5eb9202'
            'de46e5514ff39a7a65e01e485e874775ab1c0ad20b8e94ada43f4a6af1370845'
            'SKIP')

__patch_makefiles() {

    __replace_string_in_file() {
        sed -i".bak" "s/$1/$2/" "$3"
        [[ "$(cat "$3")" != "$(cat "$3.bak")" ]] || return 1
        rm "$3.bak"
    }

    __replace_string_in_file \
        "SRC_SUBDIRS = gdk gtk libgail-util modules demos tests testsuite examples" \
        "SRC_SUBDIRS = gdk gtk libgail-util modules demos" \
        "Makefile.am"

    __replace_string_in_file \
        "SUBDIRS = po po-properties \$(SRC_SUBDIRS) docs m4macros build" \
        "SUBDIRS = po \$(SRC_SUBDIRS) m4macros build" \
        "Makefile.am"

    __replace_string_in_file \
        "SUBDIRS = gtk-demo widget-factory icon-browser" \
        "SUBDIRS = widget-factory" \
        "demos/Makefile.am"

    __replace_string_in_file "SUBDIRS += cloudprint" "" "modules/printbackends/Makefile.am"
    __replace_string_in_file "SUBDIRS += cups" 	     "" "modules/printbackends/Makefile.am"
    __replace_string_in_file "gtk-update-icon-cache" "" "gtk/Makefile.am"
}

__patch_gtk_code() {
    for patchfile in $srcdir/$_repo/*.patch; do
        patch -d "gtk" -p 3 -i "$patchfile"
    done

    cat "$srcdir/$_repo/smaller-adwaita.css" >> "gtk/theme/Adwaita/gtk-contained.css"
    cat "$srcdir/$_repo/smaller-adwaita.css" >> "gtk/theme/Adwaita/gtk-contained-dark.css"
}

prepare() {
    cd gtk+-$pkgver

    # Make building faster by skipping tests, code examples and unused elements.
    __patch_makefiles

    # Apply patches to GTK code.
    __patch_gtk_code

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd gtk+-$pkgver

    CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --enable-x11-backend --enable-wayland-backend --disable-schemas-compile \
      --disable-gtk-doc-html --disable-installed-tests  --disable-maintainer-mode

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd gtk+-$pkgver

    make DESTDIR="$pkgdir" install
    install -Dm644 ../settings.ini "$pkgdir/usr/share/gtk-3.0/settings.ini"
    install -Dm644 ../gtk-query-immodules-3.0.hook "$pkgdir/usr/share/libalpm/hooks/gtk-query-immodules-3.0.hook"
    install -m644 $srcdir/$_repo/README.md $pkgdir/usr/share/gtk-3.0/README.md
}
