# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgbase=boxit
pkgname=(boxit-32 boxit-32-server)
#pkgver=2.0.$(date +%m%d)
pkgver=2.1.2
pkgrel=4
arch=('i686' 'x86_64')
url="http://git.manjaro.org"
license=('GPL')
depends=('qt5-base' 'readline' 'icu')
makedepends=('git')
_git=Yes

if [ "$_git" == "Yes" ]; then
   source=(git+https://github.com/manjaro/boxit.git#branch=32-bit
           "https://gist.githubusercontent.com/Kirek/2e227ce1b44a7f18d589c99283f32a37/raw/38eedf144ee0f1df1d56fc38395e3f18cb245c84/qt5_v2.patch")
   sha256sums=('SKIP' 'SKIP')
else
   source=("boxit-$pkgver.tar.gz::http://git.manjaro.org/core/boxit/repository/archive.tar.gz?ref=v$pkgver"
           "https://gist.githubusercontent.com/Kirek/2e227ce1b44a7f18d589c99283f32a37/raw/38eedf144ee0f1df1d56fc38395e3f18cb245c84/qt5_v2.patch")
   sha256sums=('SKIP' 'SKIP')
fi

build() {
  msg "build $pkgbase"
  if [ -e "$srcdir/core-$pkgbase" ]; then
	   cd "$srcdir/core-$pkgbase/$pkgbase/src"
  else
     cd "$srcdir/$pkgbase/$pkgbase/src"
  fi

  patch -p3 -i $srcdir/qt5_v2.patch

  qmake-qt5

  # Build fix
  make sub-DBus
  cp DBus/libDBus.a libDBus.a

  make all

  msg "build $pkgbase-server"
  if [ -e "$srcdir/core-$pkgbase" ]; then
	   cd "$srcdir/core-$pkgbase/$pkgbase-server/src"
  else
     cd "$srcdir/$pkgbase/$pkgbase-server/src"
  fi

  qmake-qt5

  make all
}

package_boxit-32() {
  pkgdesc="Manjaro Linux repository management system - client"
  depends=('qt5-base' 'readline' 'icu')
  if [ -e "$srcdir/core-$pkgbase" ]; then
	   cd "$srcdir/core-$pkgbase/$pkgbase/src"
  else
     cd "$srcdir/$pkgbase/$pkgbase/src"
  fi
  make INSTALL_ROOT="${pkgdir}" install
}

package_boxit-32-server() {
  pkgdesc="Manjaro Linux repository management system - server"
  depends=('qt5-base' 'readline' 'icu' 'boxit-server')
  if [ -e "$srcdir/core-$pkgbase" ]; then
	   cd "$srcdir/core-$pkgbase/$pkgbase-server/src"
  else
     cd "$srcdir/$pkgbase/$pkgbase-server/src"
  fi
  # TODO - convert to systemd-scripts
  #mkdir -p ${pkgdir}/etc/init.d
  #mkdir -p ${pkgdir}/usr/bin
  #mkdir -p ${pkgdir}/etc/boxit
  make INSTALL_ROOT="${pkgdir}" install

  # clean-up conflicting files
  rm "${pkgdir}"/etc/boxit -r
  rm "${pkgdir}"/usr/bin/repo*
  rm "${pkgdir}"/usr/bin/pkgdelta
}
