# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgname=xfce4-pulseaudio-plugin
pkgver=0.3.5
pkgrel=1.1
pkgdesc="Pulseaudio plugin for Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/$pkgname"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libpulse' 'libkeybinder3' 'libnotify')
makedepends=('git' 'intltool' 'python' 'xfce4-dev-tools')
optdepends=('ido: appindicator support'
    'pavucontrol: default pulseaudio mixer')
source=("http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2"
#    "https://git.xfce.org/panel-plugins/$pkgname/snapshot/$pkgname-$_commit.tar.bz2"
#    "git://git.xfce.org/panel-plugins/xfce4-pulseaudio-plugin"
    'use-panel-icons.patch')
sha256sums=('25e7bc414edf6e16140c31ca4e7dcedd4e17d40ea23a2921beb799fed11a99bb'
            '0fdf37ef543a197e0df474c7d80f0c1b77206d8a204eea37f9150f5be64fbbe1')

#pkgver() {
#    cd $pkgname
#    git describe --long --tags | sed -r "s:^${pkgname}.::;s/^v//;s/([^-]*-g)/r\1/;s/-/./g"
#}

prepare() {
    cd $pkgname-$pkgver
    patch -Np1 -i $srcdir/use-panel-icons.patch
}

build() {
    cd $pkgname-$pkgver
    ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --disable-static \
      --disable-debug \
      --enable-keybinder \
      --enable-libnotify
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install

    # clean-up as we don't use these icons
#    rm -r $pkgdir/usr/share/icons/hicolor/scalable/status/
}
