# Based on the file created for Arch Linux by: Frank Vanderham

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

_linuxprefix=linux412
_extramodules=extramodules-4.12-MANJARO
pkgname=$_linuxprefix-broadcom-wl
_pkgname=broadcom-wl
pkgver=6.30.223.271
pkgrel=0
pkgdesc='Broadcom 802.11 Linux STA wireless driver BCM43142.'
url='https://bbs.archlinux.org/viewtopic.php?id=145884'
arch=('i686' 'x86_64')
license=('custom')
depends=("$_linuxprefix")
makedepends=("broadcom-wl-dkms>=$pkgver"
             'dkms'
             "$_linuxprefix" "$_linuxprefix-headers")
groups=("$_linuxprefix-extramodules")
provides=("$_pkgname=$pkgver")
install=$_pkgname.install
backup=('etc/modprobe.d/linux412-broadcom-wl.conf')
source=(broadcom-wl-dkms.conf linux-4.12-rc1.patch)
sha256sums=('b97bc588420d1542f73279e71975ccb5d81d75e534e7b5717e01d6e6adf6a283'
            '32f9be85884ac91a7e927861fbfefe4eb0d6eee7fbae523b70eef9ef53176435')

prepare() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

  # copy this dir to local dir so we can patch it.
  cp -r -L dkms/broadcom-wl/$pkgver/source dkms/broadcom-wl/$pkgver/src
  rm dkms/broadcom-wl/$pkgver/source
  mv dkms/broadcom-wl/$pkgver/src dkms/broadcom-wl/$pkgver/source

  sed -i -e 's|rdtscl(cycles)|cycles = 0|g' dkms/broadcom-wl/$pkgver/source/src/shared/linux_osl.c
  sed -i '/skb->dev->last_rx = jiffies;/d' dkms/broadcom-wl/$pkgver/source/src/wl/sys/wl_linux.c
  sed -i 's|#include <wl_cfg80211_hybrid.h>|&\n#include <linux/sched/signal.h>|' dkms/broadcom-wl/$pkgver/source/src/wl/sys/wl_cfg80211_hybrid.c
  cd dkms/broadcom-wl/$pkgver/source
  patch -p1 -i $srcdir/linux-4.12-rc1.patch
}

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  # build host modules
  msg2 'Build module'
  dkms --dkmsframework dkms.conf build "broadcom-wl/$pkgver" -k "$_kernver"
}

package(){
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  cd "dkms/broadcom-wl/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/$_pkgname.install"
  install -D -m 644 "${srcdir}"/broadcom-wl-dkms.conf "${pkgdir}/etc/modprobe.d/${_linuxprefix}-broadcom-wl.conf"
}
