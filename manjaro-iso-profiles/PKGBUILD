# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_mt_pkgver=0.15.1

pkgbase=manjaro-iso-profiles
pkgname=('manjaro-iso-profiles-base'
        'manjaro-iso-profiles-official'
        'manjaro-iso-profiles-community')
pkgver=17.0
pkgrel=6
arch=('any')
url="https://github.com/manjaro/iso-profiles"
license=('GPL')
_git=79e264bd9c4b5e5c6cda79292b1373ff868e2ad9
source=("$url/archive/${_git}.tar.gz")
sha256sums=('c24c88f2f4e610da2cff167b4f4f8c19e02a6e1cff667a73397119187d3d77ca')

prepare(){
    # remove this
    mv $srcdir/iso-profiles-$_git $srcdir/iso-profiles-$pkgver
}

_inst_dir(){
    install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
    cp -a $srcdir/iso-profiles-$pkgver/$1 $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-base() {
    pkgdesc='Manjaro Linux iso profiles base'
    provides=('iso-profiles-base')
    conflicts=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')
    replaces=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')
    optdepends=('manjaro-iso-profiles-official: Manjaro Linux ISO-Image official profiles'
                'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles')

    _inst_dir 'shared'

    cp $srcdir/iso-profiles-$pkgver/repo_info $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-official() {
    pkgdesc='Manjaro Linux official iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")
    replaces=('manjaro-iso-profiles-minimal')

    _inst_dir 'manjaro'
}

package_manjaro-iso-profiles-community() {
    pkgdesc='Manjaro Linux community iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")

    _inst_dir 'community'
}
