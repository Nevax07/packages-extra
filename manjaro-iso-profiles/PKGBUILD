# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_mt_pkgver=0.15.2

pkgbase=manjaro-iso-profiles
pkgname=('manjaro-iso-profiles-base'
        'manjaro-iso-profiles-official'
        'manjaro-iso-profiles-community')
pkgver=17.0.1
pkgrel=3
arch=('any')
url="https://github.com/manjaro/iso-profiles"
license=('GPL')
_git=a19a911b77f424dbe531ff3c13cc23457a9e7bb2
source=("iso-profiles-$pkgver-$pkgrel.tar.gz::$url/archive/${_git}.tar.gz")
sha256sums=('ee6a2d1c33727c1a88b903c57d7e70b8712cd82eb0e6fd4145b8c22fda41060e')

prepare(){
    # remove this
    mv $srcdir/iso-profiles-$_git $srcdir/iso-profiles-$pkgver
}

_inst_dir(){
    install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
    cp -a $srcdir/iso-profiles-$pkgver/$1 $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-base() {
    pkgdesc='Manjaro Linux iso profiles base'
    provides=('iso-profiles-base')
    conflicts=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')
    replaces=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')
    optdepends=('manjaro-iso-profiles-official: Manjaro Linux ISO-Image official profiles'
                'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles')

    _inst_dir 'shared'

    cp $srcdir/iso-profiles-$pkgver/repo_info $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-official() {
    pkgdesc='Manjaro Linux official iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")
    replaces=('manjaro-iso-profiles-minimal')

    _inst_dir 'manjaro'
}

package_manjaro-iso-profiles-community() {
    pkgdesc='Manjaro Linux community iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")

    _inst_dir 'community'
}
