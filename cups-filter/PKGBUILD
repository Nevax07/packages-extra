# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=cups-filters
pkgver=1.17.9
pkgrel=4
pkgdesc="OpenPrinting CUPS Filters"
arch=('x86_64')
url="https://wiki.linuxfoundation.org/openprinting/cups-filters"
license=('custom')
depends=('lcms2' 'poppler' 'qpdf' 'imagemagick' 'liblouis' 'ijs' 'libcups' 'systemd')
makedepends=('ghostscript' 'ttf-dejavu' 'python' 'mupdf-tools') # ttf-dejavu for make check
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
	    'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'antiword: needed to convert MS Word documents (requires also docx2txt (AUR)')
conflicts=("$pkgname-dev")
replace=("$pkgname-dev")
backup=(etc/fonts/conf.d/99pdftoopvp.conf
        etc/cups/cups-browsed.conf)
source=("https://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.xz"
		'0001-utils-Enable-driverless-configuration-automagically.patch'
		'0002-utils-Fix-broken-systemd-unit.patch')
sha256sums=('98ae3f8c8309ea950144e582d252e65f4636e8812832a982edd0ab83eddef544'
            '8cdc7fc6e9baeb92dce2e459927b389db9eb6d1ae192a285901ae36f6048da54'
            'eea2c326b59e593a7a8f8fd789db4ca0fad581247d2f9e45c118d6dedf54cedb')

prepare() {
	cd $pkgname-$pkgver
	#enabling driverless configuration
	#https://dev.solus-project.com/source/cups-filters/browse/master/files/
 	patch -Np1 -i ../0001-utils-Enable-driverless-configuration-automagically.patch
	patch -Np1 -i ../0002-utils-Fix-broken-systemd-unit.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --disable-static \
    --enable-imagefilters \
    --disable-mutool \
    --with-gs-path=/usr/bin/gs \
    --with-pdftops-path=/usr/bin/gs \
    --with-rcdir=no \
    --enable-avahi \
    --with-browseremoteprotocols=DNSSD,CUPS \
    --enable-auto-setup-driverless \
    --with-test-font-path=/usr/share/fonts/TTF/DejaVuSans.ttf
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  
  # add upstream systemd support file
  install -Dm644 utils/cups-browsed.service ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
  sed -i "s|/usr/sbin/cups-browsed|/usr/bin/cups-browsed|" ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
  sed -i "s|cups.service|org.cups.cupsd.service|g" ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
  
  # use lp group from cups pkg FS#36769
  chgrp -R lp ${pkgdir}/etc/cups

  # license
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}

