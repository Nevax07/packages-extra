# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base'
        'manjaro-tools-pkg'
        'manjaro-tools-iso'
        'manjaro-tools-yaml')
pkgver=0.15.3
pkgrel=2
arch=('any')
pkgdesc='Development tools for Manjaro Linux'
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        'relro-bind-now.patch::https://github.com/manjaro/manjaro-tools/commit/b8255e9e175ed287e7d91f96fc8733b7956ebbce.patch'
        'mhwd_used.patch::https://github.com/manjaro/manjaro-tools/commit/21deb78026b2db5ad24996053b6d431327f88b02.patch'
        'oem_used.patch::https://github.com/manjaro/manjaro-tools/commit/ed0903bf3fd7472e79b2fb204fdff207cb2637ca.patch'
        'use-site-redirection.patch::https://github.com/manjaro/manjaro-tools/commit/37c3daa526b29d5d2f32bd8ee932283b9891f144.patch')
sha256sums=('965184fc77b5f01da76108012962abb4867f5d0a0276afbb458fd389df7efd9a'
            'da87197ca017ed62f61b657c165db6dc8cb3aa3af539bfbf15d5815c9c0da29c'
            '3c8de1dbe35c4e3444de8f526ef2f8602d0cba6b151133a09745945216e8b95d'
            '6fab4a29105f198b83610f51a1085ee1b1f530c35dbff9f008c7b0cd8696746b'
            'effecbfec33e183bc92221deb4d32838cabf7ae2343bb0fb73123e51ae8a5c8d')

prepare() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    # patches here
    sed -e "s/^Version=.*/Version=$pkgver/" -i Makefile
    patch -p1 -i "$srcdir/relro-bind-now.patch"
    patch -p1 -i "$srcdir/mhwd_used.patch"
    patch -p1 -i "$srcdir/oem_used.patch"
    patch -p1 -i "$srcdir/use-site-redirection.patch"
}

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
    pkgdesc='Development tools for Manjaro Linux (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman-mirrorlist>=20160301')
    optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                'manjaro-tools-iso: Manjaro Linux iso tools'
                'manjaro-tools-yaml: Manjaro Linux yaml tools')
    backup=('etc/manjaro-tools/manjaro-tools.conf')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
    pkgdesc='Development tools for Manjaro Linux (packaging tools)'
    depends=('namcap' 'manjaro-tools-base')
    conflicts=('devtools')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-yaml() {
    pkgdesc='Development tools for Manjaro Linux (yaml tools)'
    depends=('manjaro-tools-base' 'calamares-tools' 'ruby-kwalify' 'manjaro-iso-profiles-base')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}

package_manjaro-tools-iso() {
    pkgdesc='Development tools for Manjaro Linux (ISO tools)'
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'manjaro-tools-yaml'
            'mkinitcpio' 'mktorrent' 'grub')
    optdepends=('qemu: quickly test isos')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

