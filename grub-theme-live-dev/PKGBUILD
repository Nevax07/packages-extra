# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>
# Contributor: anex <assassin.anex[@]gmail.com>


pkgbase=grub-theme-live
pkgname=('grub-theme-live-common-dev' 'grub-theme-live-manjaro-dev' 'grub-theme-live-sonar' 'grub-theme-manjaro-dev')
pkgver=17.1.1
pkgrel=5
pkgdesc='Manjaro Linux grub theme'
arch=('any')
url="https://github.com/Ste74/grub-theme-dev"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

package_grub-theme-live-common-dev() {
    depends=('grub')
    conflicts=('grub-theme-live')
    replaces=('grub-theme-live')

    cd grub-theme-dev
    make PREFIX=/usr DESTDIR=${pkgdir} install_common
}
package_grub-theme-live-manjaro-dev() {
    depends=('grub-theme-live-common-dev')

    cd grub-theme-dev
    make PREFIX=/usr DESTDIR=${pkgdir} install_manjaro
}
package_grub-theme-live-sonar() {
    depends=('grub-theme-live-common')

    cd grub-theme-dev
    make PREFIX=/usr DESTDIR=${pkgdir} install_sonar
}

package_grub-theme-manjaro-dev() {
	depends=('grub')
	install=manjaro-theme.install
    
    cd grub-theme-dev/manjaro-live
    sed -i -e 's,.*text = "Welcome to Manjaro".*,#text = "Welcome to Manjaro",' theme.txt #remove welcome message
    find . -type f -exec install -D -m644 {} ${pkgdir}/usr/share/grub/themes/manjaro/{} \;
}



